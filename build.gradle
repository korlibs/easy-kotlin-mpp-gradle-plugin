buildscript {
	def isKotlinDev = kotlinVersion.contains("-release")
	def isKotlinEAp = kotlinVersion.contains("-eap")
    repositories {
		mavenCentral()
		jcenter()
        maven { url = uri("https://plugins.gradle.org/m2/") }
		if (isKotlinDev) {
			maven { url = uri("https://dl.bintray.com/kotlin/kotlin-dev") }
		}
		if (isKotlinEAp) {
			maven { url = uri("https://dl.bintray.com/kotlin/kotlin-eap") }
		}
    }
    dependencies {
        classpath("com.gradle.publish:plugin-publish-plugin:0.10.1")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
		//classpath("org.jetbrains.kotlin.jvm:org.jetbrains.kotlin.jvm.gradle.plugin:$kotlinVersion")
    }
}

plugins {
    id "java-gradle-plugin"
    //id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: "kotlin"
apply plugin: 'maven'
apply plugin: 'maven-publish'

def isKotlinDev = kotlinVersion.contains("-release")
def isKotlinEAp = kotlinVersion.contains("-eap")

repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven { url = uri("https://plugins.gradle.org/m2/") }
	if (isKotlinDev) {
		maven { url = uri("https://dl.bintray.com/kotlin/kotlin-dev") }
	}
	if (isKotlinEAp) {
		maven { url = uri("https://dl.bintray.com/kotlin/kotlin-eap") }
	}
}

dependencies {
    testImplementation("junit:junit:4.12")
	testImplementation("org.jetbrains.kotlin:kotlin-test")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-gradle-plugin")
    implementation("com.android.tools.build:gradle:3.4.1")
	implementation("com.moowork.gradle:gradle-node-plugin:1.3.1")
    compile gradleApi()
    compile localGroovy()
}


pluginBundle {
    website = "https://github.com/korlibs/easy-kotlin-mpp-gradle-plugin"
    vcsUrl = "https://github.com/korlibs/easy-kotlin-mpp-gradle-plugin"
    tags = ["korlibs", "kotlin", "kotlin-mpp"]
}

gradlePlugin {
    plugins {
        easyKotlinMppGradlePlugin {
            id = 'com.soywiz.korlibs.easy-kotlin-mpp-gradle-plugin'
            displayName = 'easy-kotlin-mpp-gradle-plugin'
            description = 'Allows to build Kotlin MPP projects easily'
            implementationClass = 'com.soywiz.korlibs.KorlibsPlugin'
        }
    }
}

def publishUser = (rootProject.findProperty("BINTRAY_USER") ?: project.findProperty("bintrayUser") ?: System.getenv("BINTRAY_USER"))?.toString()
def publishPassword = (rootProject.findProperty("BINTRAY_KEY") ?: project.findProperty("bintrayApiKey") ?: System.getenv("BINTRAY_API_KEY") ?: System.getenv("BINTRAY_KEY"))?.toString()

if (publishUser == null || publishPassword == null) {
    println("Required BINTRAY_USER and BINTRAY_KEY in ~/.gradle/gradle.properties")
}


new File(rootDir, "src/main/kotlin/com/soywiz/korlibs/KorlibsKotlinVersion.kt").write("package com.soywiz.korlibs\n\nval KORLIBS_KOTLIN_VERSION = \"${kotlinVersion}\"")

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
}

publishing {
    repositories {
		def GITHUB_REPOSITORY = System.getenv("GITHUB_REPOSITORY")
		def GITHUB_TOKEN = System.getenv("GITHUB_TOKEN")
		//def GITHUB_REPOSITORY = "korlibs/easy-kotlin-mpp-gradle-plugin"
		//def GITHUB_TOKEN = "test"
		if (GITHUB_REPOSITORY != null && GITHUB_TOKEN != null) {
			maven {
				name = "GitHubPackages"
				url = uri("https://maven.pkg.github.com/${GITHUB_REPOSITORY}")
				credentials(HttpHeaderCredentials) {
					name = "authorization"
					value = "Bearer ${GITHUB_TOKEN}"
				}
			}
		}

		if (publishUser && publishPassword) {
			maven {
				credentials {
					username = publishUser
					password = publishPassword
				}
				url = uri("https://api.bintray.com/maven/${project.property("project.bintray.org")}/${project.property("project.bintray.repository")}/${project.property("project.bintray.package")}/")
			}
		}
    }
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)

            pom {
                name = project.name
                description = project.property("project.description")
                url = project.property("project.scm.url")
                licenses {
                    license {
                        name = project.property("project.license.name")
                        url = project.property("project.license.url")
                    }
                }
                scm {
                    url = project.property("project.scm.url")
                }
            }
        }
    }
}

publish.doLast {
	def subject = project.property("project.bintray.org")
	def repo = project.property("project.bintray.repository")
	def _package = project.property("project.bintray.package")
	def version = project.version

	((HttpURLConnection)new URL("https://bintray.com/api/v1/content/$subject/$repo/$_package/$version/publish").openConnection()).with({
		requestMethod = 'POST'
		doOutput = true

		setRequestProperty("Authorization", "Basic " + "$publishUser:$publishPassword".bytes.encodeBase64().toString())
		outputStream.withPrintWriter({printWriter ->
			printWriter.write('{"discard": false, "publish_wait_for_secs": -1}')
		})
		System.out.println(inputStream.text)
	})
}
